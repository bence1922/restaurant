plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.openapi.generator" version "7.0.1"
	id "io.swagger.core.v3.swagger-gradle-plugin" version "2.2.18"
}

group = 'bme.restaurant'
version = '0.0.1-SNAPSHOT'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.18'
	implementation 'io.swagger.parser.v3:swagger-parser-v3:2.1.18'
	implementation 'org.openapitools:openapi-generator-gradle-plugin:7.0.1'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	implementation 'javax.annotation:javax.annotation-api:1.2-b01'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'javax.xml.bind:jaxb-api:2.3.1'

	implementation 'org.mindrot:jbcrypt:0.4'

	compileOnly 'javax.servlet:servlet-api:2.5'
}

repositories {
	mavenCentral()
}

java {
	sourceCompatibility = '17'
}

configurations {
	all {
		exclude group: 'ch.qos.logback', module: 'logback-classic'
	}
}

openApiGenerate {
  generatorName = "spring"
  inputSpec = "$rootDir/src/main/resources/restaurant-api-doc.yaml".toString()
  outputDir = "$rootDir/build/generated/openapi".toString()
  apiPackage = "bme.restaurant.api"
  modelPackage = "bme.restaurant.dto"
  modelNameSuffix = "DTO"
  configOptions = [
      dateLibrary: "java8",
	  interfaceOnly: "true",
	  useSpringBoot3: "true"
  ]
}

tasks.register('openApiCopy', Copy) {
    from layout.buildDirectory.dir("$rootDir/build/generated/openapi/src/main/java/bme/restaurant")
    into layout.buildDirectory.dir("$rootDir/src/main/java/bme/restaurant")
}

tasks.register('openApiClean', Delete) {
	delete fileTree("$rootDir/build/generated/openapi")
    delete fileTree("$rootDir/src/main/java/bme/restaurant/api")
    delete fileTree("$rootDir/src/main/java/bme/restaurant/dto")
}

tasks.register('openApiRegenerate') {
    dependsOn 'openApiClean', 'openApiGenerate', 'openApiCopy'

	tasks.findByName('openApiGenerate').mustRunAfter 'openApiClean'
    tasks.findByName('openApiCopy').mustRunAfter 'openApiGenerate'
}